cmake_minimum_required(VERSION 3.15)

project(HazelEditor VERSION 0.1.0 LANGUAGES CXX)
DeclareProjectInstallDirectories()

add_executable(HazelEditor)
add_subdirectory(src)
target_link_libraries(HazelEditor
    PRIVATE
        Hazel::Hazel
        Hazel::BuildFlags
)

set_target_properties(HazelEditor
    PROPERTIES
        MSVC_RUNTIME_LIBRARY MultiThreaded$<$<CONFIG:Debug>:Debug>$<$<BOOL:${BUILD_SHARED_LIBS}>:DLL>
        RUNTIME_OUTPUT_DIRECTORY                ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/HazelEditor
        RUNTIME_OUTPUT_DIRECTORY_DEBUG          ${CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG}/HazelEditor
        RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO}/HazelEditor
        RUNTIME_OUTPUT_DIRECTORY_RELEASE        ${CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE}/HazelEditor
        ARCHIVE_OUTPUT_DIRECTORY                ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}/HazelEditor
        ARCHIVE_OUTPUT_DIRECTORY_DEBUG          ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG}/HazelEditor
        ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO}/HazelEditor
        ARCHIVE_OUTPUT_DIRECTORY_RELEASE        ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE}/HazelEditor
        LIBRARY_OUTPUT_DIRECTORY                ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/HazelEditor
        LIBRARY_OUTPUT_DIRECTORY_DEBUG          ${CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG}/HazelEditor
        LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO ${CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO}/HazelEditor
        LIBRARY_OUTPUT_DIRECTORY_RELEASE        ${CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE}/HazelEditor
)

InstallProject(TARGETS HazelEditor)

# Copy Hazel dll into HazelEditor build directory
add_custom_command(
    TARGET HazelEditor POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        $<TARGET_FILE:Hazel>
        $<TARGET_FILE_DIR:HazelEditor>
    DEPENDS Hazel
    VERBATIM
    USES_TERMINAL
    COMMAND_EXPAND_LISTS
)
add_custom_command(
    TARGET HazelEditor POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        ${Sandbox_SOURCE_DIR}/assets
        $<TARGET_FILE_DIR:HazelEditor>/assets
    DEPENDS Hazel
    VERBATIM
    USES_TERMINAL
    COMMAND_EXPAND_LISTS
)
